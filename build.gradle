apply plugin: 'java'
apply plugin: 'maven'
//apply plugin: 'maven-publish'   expiramental
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'signing'

group = 'com.github.abrarsyed'
version = '1.0-SNAPSHOT'
archivesBaseName = 'jAstyle'

repositories {
	 mavenCentral()
}

configurations {
	 deployJars
	 provided	
	  
	 eclipse.classpath.plusConfigurations += provided
	 sourceSets.main.compileClasspath += provided
	 sourceSets.test.compileClasspath += provided
}

dependencies
{
	provided 'org.projectlombok:lombok:0.11.8'
	
	testCompile 'junit:junit:4.5'

	deployJars 'org.apache.maven.wagon:wagon-http:2.2'
}

/*
 * maven-publish not implemented yet.
publications {
	mavenJava(MavenPublication) {
		artifact jar
		artifact javadocJar
		artifact sourcesjard
	}
	repositories {
		maven {
			 url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			 
			  }
	}
}
*/

jar {
	metaInf {
		from 'LICENSE'
		from 'README'
	}
	from sourceSets.main.resources

	// manifest
	manifest { attributes 'Main-Class' : 'com.github.abrarsyed.jastyle.Main' }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing {
	
	 sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			
			configuration = configurations.deployJars
			beforeDeployment { MavenDeployment deployment ->
				 signing.signPom(deployment)
			}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom {
				groupId = group
				version = version
				artifactId = archivesBaseName
				project {
					name 'jAstyle'
					packaging 'jar'
					description 'Java Artistic Style Library'
					url 'https://github.com/AbrarSyed/jastyle'

					scm {
						url 'https://github.com/AbrarSyed/jastyle'
						connection 'scm:git:git://github.com/AbrarSyed/jastyle.git'
						developerConnection 'scm:git:git@github.com:AbrarSyed/jastyle.git'
					}

					issueManagement {
						system 'github'
						url 'http://github.com/AbrarSyed/jastyle/issues'
					}

					licenses {
						license {
							name 'GNU Lesser General Public License (LGPL), Version 3'
							url 'http://www.fsf.org/licensing/licenses/lgpl.txt'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'stephenc'
							name 'Stephen Connolly'
							roles {
								role 'packager'
							}
						}
						developer {
							name 'Hector Suarez Barenca'
							roles {
								role 'developer'
							}
						}
						developer {
							id 'AbrarSyed'
							name 'Abrar Syed'
							roles {
								role 'packager'
								role 'developer'
							}
						}
					}
				}
			}
		}
	}
}
